#version 460


layout (location = 0) out vec4 FragColor;
layout (location = 1) out vec4 BrightColor;

struct ColorComponent{
	sampler2D sampler;
	vec4 uv;
	vec3 color;
};

struct Material{
	ColorComponent diffuse;
	ColorComponent specular;
	ColorComponent normalMap;
	float shininess;
	float transparency;
};

struct DirectionalLight {
    vec3 direction;
    vec3 ambient;
    vec3 diffuse;
    vec3 specular;
	int used;
};

struct PointLight {
    vec3 position;
    vec3 ambient;
    vec3 diffuse;
    vec3 specular;
	float constant;
	float linear;
	float quadratic;
	int used;
};

struct SpotLight {
    vec3 position;
	vec3 direction;
    vec3 ambient;
    vec3 diffuse;
    vec3 specular;
	float cutOff;
	float outerCutOff;
	float constant;
	float linear;
	float quadratic;
	int used;
};

#define NR_POINT_LIGHTS 10
#define NR_SPOT_LIGHTS 10
#define NR_DIR_LIGHTS 10


in vec3 FragmentPosition;
in vec4 ShadowPosition;
in vec3 FragmentUV;
in mat3 TBN;

uniform PointLight pointLights[NR_POINT_LIGHTS];
uniform SpotLight spotLights[NR_POINT_LIGHTS];
uniform DirectionalLight directionalLights[NR_DIR_LIGHTS]; 
uniform vec3 viewPosition; 
uniform Material material;

vec3 specular;
vec3 diffuse;
vec3 ambient;
vec3 normal;
vec3 TBNfragPos;
vec3 TBNviewPos;
vec3 TBNlightPos;
void CalculateDirectionalLights(vec3 normal, vec3 viewDirection);
void CalculatePointLights( vec3 normal,vec3 viewDirection, vec3 fragmentPosition);
void CalculateSpotLights(vec3 normal,vec3 viewDirection, vec3 fragmentPosition);

void main()
{
	
	//----------------------------------------------------------------------------------------------------------------------initial setup
	//vec3 normal = normalize(vec3(0,0,-1));
	
	//if( material.normalMap.uv.z !=0 && material.normalMap.uv.w !=0){
		
		normal = texture(material.normalMap.sampler, vec2(material.normalMap.uv.x + (material.normalMap.uv.z*FragmentUV.x),material.normalMap.uv.y + (material.normalMap.uv.w*FragmentUV.y))).rgb;
		normal = normalize(normal * 2.0 - 1.0);  
	
	//}
	//else normal = normalize(vec3(0,0,-1));
	 
	TBNfragPos = FragmentPosition * TBN;
	TBNviewPos = viewPosition * TBN;
	
	//vec3 viewDirection = normalize(vec3(0,0,-1));
	vec3 viewDirection = normalize(TBNviewPos - TBNfragPos);
	vec4 diffTexture = texture(material.diffuse.sampler, vec2(material.diffuse.uv.x + (material.diffuse.uv.z*FragmentUV.x),material.diffuse.uv.y + (material.diffuse.uv.w*FragmentUV.y)));
	
	
	vec3 matDiff = vec3(diffTexture)* material.diffuse.color; 
	vec3 matSpec = vec3(texture(material.specular.sampler, vec2(material.specular.uv.x + (material.specular.uv.z*FragmentUV.x),material.specular.uv.y + (material.specular.uv.w*FragmentUV.y)))) * material.specular.color ; 
	
	
	ambient = vec3(0,0,0);
	diffuse = vec3(0,0,0);
	specular = vec3(0,0,0);
	
	//----------------------------------------------------------------------------------------------------------------------directional
    CalculateDirectionalLights( normal, viewDirection);
	
	//----------------------------------------------------------------------------------------------------------------------point lights
	CalculatePointLights(normal, viewDirection, FragmentPosition);

	//----------------------------------------------------------------------------------------------------------------------spot lights
	CalculateSpotLights(normal, viewDirection,FragmentPosition);
		
	//----------------------------------------------------------------------------------------------------------------------result
	
	ambient *= matDiff;
	diffuse *= matDiff;
	specular *= matSpec;
		
	//FragColor =  vec4((ambient + specular + diffuse), material.transparency);
	FragColor =  vec4((ambient + specular + diffuse), diffTexture.w*material.transparency) ;
	
	float brightness = dot(FragColor.rgb, vec3(0.2126,0.7152,0.0722));
	if(brightness > 1.0)
		BrightColor = vec4(FragColor.rgb, 1.0);
	else 
		BrightColor = vec4(0.0,0.0,0.0,1.0);
	
	
	
	
	
} 


void CalculateDirectionalLights(vec3 normal, vec3 viewDirection)
{
	
	vec3 lightDirection;
	vec3 reflectionDirection;
	
	float diff;
	float specularStrength;
	
	DirectionalLight light;
	
	
	for(int i = 0; i < NR_DIR_LIGHTS ; i++)
	{
		light = directionalLights[i];
		
		if(light.used == 0) continue;
			
			
		
			//----------------------------------------------------------------------------------------------------------------------initial setup
			lightDirection = normalize( (light.direction * TBN) - TBNfragPos);
				
			//----------------------------------------------------------------------------------------------------------------------ambient
			ambient += light.ambient * material.diffuse.color ;
			
			//----------------------------------------------------------------------------------------------------------------------diffuse

			diff = max(dot(normal, lightDirection), 0.0);
			diffuse += diff  * light.diffuse * material.diffuse.color;

			//----------------------------------------------------------------------------------------------------------------------specular
			reflectionDirection = reflect(-lightDirection, normal);
			specularStrength = pow(max(dot(viewDirection, reflectionDirection),0.0),material.shininess);
			
			specular += light.specular * specularStrength;
		
	}
		//----------------------------------------------------------------------------------------------------------------------result
	
	
}

void CalculatePointLights(vec3 normal,vec3 viewDirection, vec3 fragmentPosition)
{
	
	vec3 lightDirection;
	vec3 reflectionDirection;
	
	float distance ;
	float attenuation;
	float diff;
	float specularStrength;
	
	PointLight light;
	
	
	
	for(int i = 0; i < NR_POINT_LIGHTS ; i++)
	{
		light = pointLights[i];
		if(light.used == 0) continue;	
		
		//----------------------------------------------------------------------------------------------------------------------initial setup
		lightDirection = normalize((light.position * TBN) - TBNfragPos);
		distance = length(light.position - fragmentPosition);
		attenuation = 1.0 / (light.constant + light.linear * distance + light.quadratic * (distance*distance));
		
		//----------------------------------------------------------------------------------------------------------------------ambient
		ambient += light.ambient  * attenuation  ;
		
		//----------------------------------------------------------------------------------------------------------------------diffuse
		diff = max(dot(normal, lightDirection), 0.0);
		diffuse += diff   * light.diffuse  *  attenuation ;

		//----------------------------------------------------------------------------------------------------------------------specular
		reflectionDirection = reflect(-lightDirection, normal);
		specularStrength = pow(max(dot(viewDirection, reflectionDirection),0.0),material.shininess);
		specular += light.specular * specularStrength * attenuation ;	
	}
	

	
}

void CalculateSpotLights( vec3 normal,vec3 viewDirection, vec3 fragmentPosition)
{
	
	vec3 lightDirection;
	vec3 reflectionDirection;
	
	float theta;
	float epsilon;
	float intensity;
	float diff;
	float specularStrength;
	float distance ;
	float attenuation;
	
	SpotLight light;
	
	for(int i = 0; i < NR_SPOT_LIGHTS ; i++)
	{
		light = spotLights[i];
		if(light.used == 0) continue;	
		
		//----------------------------------------------------------------------------------------------------------------------initial setup
		lightDirection = normalize((light.position*TBN) - TBNfragPos);
		distance = length(light.position - fragmentPosition);
		attenuation = 1.0 / (light.constant + light.linear * distance + light.quadratic * (distance*distance));
		
		//----------------------------------------------------------------------------------------------------------------------angle calculations
		theta = dot(lightDirection, normalize((-light.direction * TBN)));
		epsilon = light.cutOff - light.outerCutOff;
		intensity = clamp((theta - light.outerCutOff)/epsilon, 0.0,1.0);
		
		//----------------------------------------------------------------------------------------------------------------------ambient
		ambient += light.ambient * attenuation * intensity;
		
			
		//----------------------------------------------------------------------------------------------------------------------diffuse
		diff = max(dot(normal, lightDirection), 0.0);
		diffuse += diff   * light.diffuse * intensity * attenuation ;

		//----------------------------------------------------------------------------------------------------------------------specular
		reflectionDirection = reflect(-lightDirection, normal);
		specularStrength = pow(max(dot(viewDirection, reflectionDirection),0.0),material.shininess);
		specular += light.specular * specularStrength * intensity * attenuation ;

	}
	//----------------------------------------------------------------------------------------------------------------------result
	
}